struct CalendarGadget {
	// A UIGadget must always be the first member of a gadget
	UIGadget gadget;

	// Default event handlers
	void onopen();
	void ondraw();

	// Internal methods
	void DrawCalendar(bool calendarFlag);
	void SetCalendarTheme(int frmR, int frmG, int frmB,
							int bgR, int bgG, int bgB,
							int shdR, int shdG, int shdB,
							int txtR, int txtG, int txtB);
	void SetCalendar(bool expandedView, bool gridFlag);
	void SetCalendarBackground(int colorDepth, int background, int backgroundColorIndex);
	void SetCalendarButtons(UIButton calendarBtn);
	void DrawCalendarGrid(bool startOnSunday);
	void DrawMoreInfo();
	void DrawBackground();
	void DrawBuffer();

	// Internal data
	Bitmap calBMP;
	int 	border;
	Draw	calBuffer;
	Draw 	draw;
    Date	today, temp, startOfYear, endOfYear;
	UIButton	mainCalButton, calendarButton, moreButton;
	bool	drawBGRectangle;
	bool	calFlag;
};


// Color theme variables
int caBorderR, caBorderG, caBorderB,
	caBackgroundR, caBackgroundG, caBackgroundB,
	caShadowR, caShadowG, caShadowB,
	caTextR, caTextG, caTextB;
int caColorDepth, caBackground, caBackgroundColorIndex;

void CalendarGadget.SetCalendarTheme(int frmR, int frmG, int frmB,
									int bgR, int bgG, int bgB,
									int shdR, int shdG, int shdB,
									int txtR, int txtG, int txtB) {
	caBorderR = frmR;
	caBorderG = frmG;
	caBorderB = frmB;
	
	caBackgroundR = bgR;
	caBackgroundG = bgG;
	caBackgroundB = bgB;
	
	caShadowR = shdR;
	caShadowG = shdG;
	caShadowB = shdB;
	
	caTextR = txtR;
	caTextG = txtG;
	caTextB = txtB;
}


// Set background variables
void CalendarGadget.SetCalendarBackground(int colorDepth, int background, int backgroundColorIndex) {
	caColorDepth = colorDepth;
	caBackground = background;
	caBackgroundColorIndex = backgroundColorIndex;
}


void CalendarGadget.DrawBackground() {
	if (caColorDepth > 4) {
		switch (caBackground) {
			case 1:
				// form background: Day
				calBuffer.begin();
				calBuffer.bgRGB(204, 255, 153);
				calBuffer.rect(clrBG, 0, 0, gadget.w, gadget.h, 0);
				calBuffer.bgRGB(153, 204, 255);
				calBuffer.rect(clrBG, 0, 0, gadget.w, 60, 0);
				calBuffer.bgRGB(255, 255, 153);
				calBuffer.rect(clrBG, -47, -50, 57, 50, 50);
				calBuffer.end();
				break;			
			case 2:
				// form background: Sunrise
				calBuffer.begin();
				calBuffer.bgRGB(255, 153, 0);
				calBuffer.rect(clrBG, 0, 0, gadget.w, gadget.h, 0);
				calBuffer.bgRGB(255, 255, 0);
				calBuffer.rect(clrBG, 90, 20, 190, 120, 50);
				calBuffer.bgRGB(153, 0, 0);
				calBuffer.rect(clrBG, 0, 60, gadget.w, gadget.h, 0);
				calBuffer.end();
				break;
			case 3:
				// form background: Sunset
				calBuffer.begin();
				calBuffer.bgRGB(255, 153, 204);
				calBuffer.rect(clrBG, 0, 0, gadget.w, gadget.h, 0);
				calBuffer.bgRGB(255, 255, 0);
				calBuffer.rect(clrBG, -50, 20, 50, 120, 50);
				calBuffer.bgRGB(50, 50, 102);
				calBuffer.rect(clrBG, 0, 60, gadget.w, gadget.h, 0);
				calBuffer.end();
				break;
			case 4:
				// form background: Night
				calBuffer.begin();
				calBuffer.bgRGB(51, 51, 102);
				calBuffer.rect(clrBG, 0, 0, gadget.w, gadget.h, 0);
				calBuffer.bgRGB(51, 102, 153);
				calBuffer.rect(clrBG, 0, 0, gadget.w, 60, 0);
				calBuffer.bgRGB(255, 255, 204);
				calBuffer.rect(clrBG, 40, -40, 120, 40, 40);
				calBuffer.end();
				break;
			case 5:
				// form background: Custom...
				calBuffer.begin();
				calBuffer.fg(caBackgroundColorIndex);
				calBuffer.rect(clrFG, 0, 0, gadget.w, gadget.h, 0);
				calBuffer.end();
				break;
		}
	}
}


void CalendarGadget.DrawCalendar(bool calendarFlag) {
	border = 8;
	
	DrawBackground();
	
	if (calendarFlag) {
		calBuffer.begin();		
		// Shadow
		calBuffer.fgRGB(caShadowR, caShadowG, caShadowB);
		calBuffer.frame(clrFG, border+3, border+3,
					gadget.w-(border+2), gadget.h-(border+2), 5, 2);
		
		// Background
		calBuffer.fgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
		calBuffer.rect(clrFG, border+3, border+3,
					gadget.w-(border+2), gadget.h-(border+2), 5);
		
		// Border
		calBuffer.fgRGB(caBorderR, caBorderG, caBorderB);
		calBuffer.frame(clrFG, border+3, border+3,
					gadget.w-(border+4), gadget.h-(border+4), 5, 3);
		
		// Calendar icon behind date
		calBuffer.bgRGB(0, 0, 0);
		calBuffer.rect(clrBG, 16, 16, 37, 40, 3);
		
		calBuffer.bgRGB(255, 255, 255);
		calBuffer.rect(clrBG, 15, 15, 36, 39, 3);
		
		calBuffer.bgRGB(255, 0, 0);
		calBuffer.rect(clrBG, 15, 15, 36, 21, 3);
		calBuffer.rect(clrBG, 15, 18, 36, 21, 0);
		calBuffer.bgRGB(153, 0, 0);
		calBuffer.rect(clrBG, 17, 17, 19, 19, 1);
		calBuffer.rect(clrBG, 20, 17, 22, 19, 1);
		calBuffer.rect(clrBG, 23, 17, 25, 19, 1);
		calBuffer.rect(clrBG, 26, 17, 28, 19, 1);
		calBuffer.rect(clrBG, 29, 17, 31, 19, 1);
		calBuffer.rect(clrBG, 32, 17, 34, 19, 1);
		
		
		// Details around button
		calBuffer.rect(clrFG, gadget.w-45, gadget.h-32,
					gadget.w-9, gadget.h-9, 8);
		
		calBuffer.fgRGB(caBorderR, caBorderG, caBorderB);
		
		calBuffer.line(clrFG, gadget.w-16, gadget.h-32,
						gadget.w-13, gadget.h-32);
		calBuffer.line(clrFG, gadget.w-15, gadget.h-33,
						gadget.w-13, gadget.h-33);
		calBuffer.line(clrFG, gadget.w-14, gadget.h-34,
						gadget.w-13, gadget.h-34);
		calBuffer.line(clrFG, gadget.w-13, gadget.h-35,
						gadget.w-13, gadget.h-35);
		
		calBuffer.line(clrFG, gadget.w-48, gadget.h-13,
						gadget.w-45, gadget.h-13);
		calBuffer.line(clrFG, gadget.w-47, gadget.h-14,
						gadget.w-45, gadget.h-14);
		calBuffer.line(clrFG, gadget.w-46, gadget.h-15,
						gadget.w-45, gadget.h-15);
		
		calBuffer.end();
	}
}


// Draw date in calendar
void CalendarGadget.SetCalendar(bool expandedView, bool gridFlag) {
	int dayNum, monthNum, dateOnCalendar;
	string dayStr, monthStr, monthYear;
	today.now();
	
	// Weekday
	dayNum = today.weekday;
	switch (dayNum) {
		case 0:
			dayStr = "Sunday         ";
			break;
		case 1:
			dayStr = "Monday        ";
			break;
		case 2:
			dayStr = "Tuesday       ";
			break;
		case 3:
			dayStr = "Wednesday ";
			break;
		case 4:
			dayStr = "Thursday     ";
			break;
		case 5:
			dayStr = "Friday            ";
			break;
		case 6:
			dayStr = "Saturday     ";
			break;
		default:
			dayStr = "---            ";
	}
	
	// Month string
	monthNum = today.month;
	
	if (expandedView) {
		switch (monthNum) {
			case 1:
				monthStr = "January";
				break;
			case 2:
				monthStr = "February";
				break;
			case 3:
				monthStr = "March";
				break;
			case 4:
				monthStr = "April";
				break;
			case 5:
				monthStr = "May";
				break;
			case 6:
				monthStr = "June";
				break;
			case 7:
				monthStr = "July";
				break;
			case 8:
				monthStr = "August";
				break;
			case 9:
				monthStr = "September";
				break;
			case 10:
				monthStr = "October";
				break;
			case 11:
				monthStr = "November";
				break;
			case 12:
				monthStr = "December";
				break;
			default:
				monthStr = "---";
				break;
		}
	}
	else 
	{
		switch (monthNum) {
			case 1:
				monthStr = "Jan.";
				break;
			case 2:
				monthStr = "Feb.";
				break;
			case 3:
				monthStr = "Mar.";
				break;
			case 4:
				monthStr = "Apr.";
				break;
			case 5:
				monthStr = "May.";
				break;
			case 6:
				monthStr = "Jun.";
				break;
			case 7:
				monthStr = "Jul.";
				break;
			case 8:
				monthStr = "Aug.";
				break;
			case 9:
				monthStr = "Sep.";
				break;
			case 10:
				monthStr = "Oct.";
				break;
			case 11:
				monthStr = "Nov.";
				break;
			case 12:
				monthStr = "Dec.";
				break;
			default:
				monthStr = "---";
				break;
		}
	}
		
	// Month + year
	monthYear = monthStr + ", " + today.year + "   ";
	
	// Draw date on calendar
	calBuffer.begin();
	calBuffer.bgRGB(255, 255, 255);
	calBuffer.textRGB(0, 51, 102);
	calBuffer.font(fntLargeBold);
	calBuffer.textAlign(11);
	
	dateOnCalendar = today.day;
	
	if (strlen(dateOnCalendar) == 1) {
		if (gadget.w == 160) {
			calBuffer.text(clrText, 25, 29, "  " + dateOnCalendar + "  ");
		}
		else
		{
			calBuffer.text(clrText, 17, 21, "  " + dateOnCalendar + "  ");
		}
	}
	else
	{
		if (gadget.w == 160) {
			calBuffer.text(clrText, 23, 29, strleft(dateOnCalendar, 1) + " ");
			calBuffer.text(clrText, 29, 29, strright(dateOnCalendar, 1));
		}
		else
		{
			calBuffer.text(clrText, 15, 21, strleft(dateOnCalendar, 1)  + " ");
			calBuffer.text(clrText, 21, 21, strright(dateOnCalendar, 1));
		}
	}

	// Draw weekday
	calBuffer.bgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
	//calBuffer.bgRGB(0, 0, 0);
	calBuffer.textRGB(caTextR, caTextG, caTextB);
	calBuffer.font(fntStandard);
	calBuffer.textAlign(00);
	
	if (gadget.w == 160) {
		calBuffer.text(clrText, 40, 16, dayStr);
	}
	else
	{
		calBuffer.text(clrText, 32, 8, dayStr);
	}
	
	// Draw month and year
	calBuffer.font(fntBold);
	calBuffer.textAlign(00);
	
	if (gadget.w == 160) {
		calBuffer.text(clrText, 40, 27, monthYear);
	}
	else
	{
		calBuffer.text(clrText, 32, 19, monthYear);
	}
	
	// Draw calendar drid
	if (expandedView) {
		DrawCalendarGrid(gridFlag);
	}
	
	drawBGRectangle = true;

	calBuffer.end();
}


void CalendarGadget.DrawCalendarGrid(bool startOnSunday) {
	
	// Function to draw calendar, receives a boolean parameter
	// If startOnSunday is true, week starts on Sunday; otherwise, starts on Monday
	
    // Variable and object declarations
    int x, y;
    int startX, startY;
    int col, row;
    int day, daysInMonth, firstWeekday, todayDay;
    int calendarW, calendarH;
    
    // Main code starts here
    today.now();
    todayDay = today.day;

    // Get calendar gadget dimensions
    calendarW = gadget.w;
    calendarH = gadget.h;

    // Get the first day of the month
    temp = today;
    temp.ymd = (today.year * 10000) + (today.month * 100) + 1;

    // Determine what weekday the first day falls on
    firstWeekday = temp.weekday; // 0 = Sunday, 1 = Monday, ..., 6 = Saturday

    // Adjust firstWeekday depending on startOnSunday flag
    if (!startOnSunday) {
        // Shift so that Monday becomes 0, Sunday becomes 6
        firstWeekday = (firstWeekday + 6) % 7;
    }

    // Get the number of days in this month
    temp.adddays(32);
    temp.ymd = (temp.year * 10000) + (temp.month * 100) + 1;
    temp.subdays(1);
    daysInMonth = temp.day;

    // Calculate starting position for drawing
    startX = (calendarW/2) - 45;
    startY = (calendarH/2) - 20;

    calBuffer.begin();
    calBuffer.textAlign(11);
    calBuffer.font(fntBold);
    calBuffer.bgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
    calBuffer.textRGB(caTextR, caTextG, caTextB);
    
    // Background rectangle
    calBuffer.fgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
    calBuffer.rect(clrFG, 26, 45, 134, 122, 0);

    // Draw weekday headers based on startOnSunday flag
    if (startOnSunday) {
    	calBuffer.textRGB(255, 51, 51);
        calBuffer.text(clrText, (calendarW/2)-45, (calendarH/2)-30, "S");
        calBuffer.textRGB(caTextR, caTextG, caTextB);
        calBuffer.text(clrText, (calendarW/2)-30, (calendarH/2)-30, "M");
        calBuffer.text(clrText, (calendarW/2)-15, (calendarH/2)-30, "T");
        calBuffer.text(clrText, (calendarW/2),    (calendarH/2)-30, "W");
        calBuffer.text(clrText, (calendarW/2)+15, (calendarH/2)-30, "T");
        calBuffer.text(clrText, (calendarW/2)+30, (calendarH/2)-30, "F");
        calBuffer.textRGB(255, 51, 51);
        calBuffer.text(clrText, (calendarW/2)+45, (calendarH/2)-30, "S");
        calBuffer.textRGB(caTextR, caTextG, caTextB);
    } else {
        calBuffer.text(clrText, (calendarW/2)-45, (calendarH/2)-30, "M");
        calBuffer.text(clrText, (calendarW/2)-30, (calendarH/2)-30, "T");
        calBuffer.text(clrText, (calendarW/2)-15, (calendarH/2)-30, "W");
        calBuffer.text(clrText, (calendarW/2),    (calendarH/2)-30, "T");
        calBuffer.text(clrText, (calendarW/2)+15, (calendarH/2)-30, "F");
        calBuffer.textRGB(255, 51, 51);
        calBuffer.text(clrText, (calendarW/2)+30, (calendarH/2)-30, "S");
        calBuffer.text(clrText, (calendarW/2)+45, (calendarH/2)-30, "S");
        calBuffer.textRGB(caTextR, caTextG, caTextB);
    }

    // Start drawing day numbers
    col = firstWeekday;
    row = 0;

    calBuffer.font(fntStandard);

    for (day = 1; day <= daysInMonth; day++) {
        x = startX + (col * 15);
        y = startY + (row * 11);
		
        // Highlight today in red background and white bold text
        if (day == todayDay) {
            calBuffer.bgRGB(255, 0, 0);
            calBuffer.textRGB(255, 255, 255);
            calBuffer.font(fntBold);
            
            calBuffer.rect(clrBG, x-8, y-4, x+7, y+7, 3);
            calBuffer.text(clrText, x, y, day);
            
            calBuffer.font(fntStandard);  // Reset font for following days
            calBuffer.bgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
            calBuffer.textRGB(caTextR, caTextG, caTextB);
        } else {
            calBuffer.text(clrText, x, y, day);
        }

        // Move to next column/row
        col++;
        if (col > 6) {
            col = 0;
            row++;
        }
    }
    
	calBuffer.end();
	
	DrawMoreInfo();
}


void CalendarGadget.DrawMoreInfo() {
    string infoText;
    int dayOfYear, daysLeft, weekNumber, x, x2, y;

	// Day of the current year
	startOfYear.ymd = (today.year * 10000) + 101;  // january 1st
	dayOfYear = today.diffdays(startOfYear) + 1;

	// Days left on current year
	endOfYear.ymd = (today.year * 10000) + 1231;  // december 31st
	daysLeft = endOfYear.diffdays(today);
	
	// Week number
	weekNumber = (dayOfYear - 1) / 7 + 1;
	
	x = 21;
	x2 = 86;
	y = 121;

	calBuffer.begin();
	
    // Background rectangle
    //calBuffer.fgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
    //if (drawBGRectangle) calBuffer.rect(clrFG, 26, 45, 134, 122, 0);
    
    calBuffer.bgRGB(caBackgroundR, caBackgroundG, caBackgroundB);
    calBuffer.textRGB(caTextR, caTextG, caTextB);
    
    calBuffer.font(fntStandard);
    calBuffer.textAlign(00);
	infoText = "Day/Week: ";
	calBuffer.text(clrText, x, y, infoText);
	
	calBuffer.font(fntBold);
	calBuffer.textAlign(02);
	calBuffer.textRGB(255, 0, 0);
	calBuffer.text(clrText, x+x2, y, dayOfYear + "/" + weekNumber);
	
	/*calBuffer.textRGB(caTextR, caTextG, caTextB);
	calBuffer.font(fntStandard);
	calBuffer.textAlign(00);
	infoText = "Week number:";
	calBuffer.text(clrText, x, y+10, infoText);*/
	
	/*calBuffer.font(fntBold);
	calBuffer.textAlign(02);
	calBuffer.textRGB(255, 0, 0);
	calBuffer.text(clrText, x+x2, y+10, weekNumber);*/
	
	calBuffer.textRGB(caTextR, caTextG, caTextB);
	calBuffer.font(fntStandard);
	calBuffer.textAlign(00);
	infoText =  "Days left: ";
	calBuffer.text(clrText, x, y+10, infoText);
	
	calBuffer.font(fntBold);
	calBuffer.textAlign(02);
	calBuffer.textRGB(255, 0, 0);
	calBuffer.text(clrText, x+x2, y+10, daysLeft);
    
    calBuffer.end();
}


/*void CalendarGadget.SetCalendarObjects(Bitmap calendarBMP, UIButton calendarBtn) {
	calBMP = calendarBMP;
	mainCalButton = calendarBtn;
}*/


void CalendarGadget.SetCalendarButtons(UIButton calendarBtn) {
	calendarButton = calendarBtn;
}


// Draw the buffer on screen
void CalendarGadget.DrawBuffer() {
	draw.begin();
	draw.draw(calBuffer, gadget.x, gadget.y);
	draw.end();
}


void CalendarGadget.onopen() {
	calBuffer.create(160, 160);
	
	draw.attachGadget(gadget); 
}


void CalendarGadget.ondraw() {
	// BUFFER:
	// Create offscreen buffer
	/*if (gadget.w == 144) {
		buffer.create(160, 160); // Fullscreen calendar size
		buffer.release();
	}
	else
	{
		buffer.create(101, 39); // Overview calendar size
		buffer.release();
	}*/
	
	//DrawCalendar(false);
	
	DrawBuffer();
	
	if (gadget.w < 160) {
		SetCalendar(false, true);
		/*mainCalButton.visible = true;*/
	}
	else
	{
		calendarButton.visible = true;
	}
}
